import "Lava.del";

globalvar define ball1;
globalvar define ball2;
playervar define isBall;
globalvar define ballIsOnStand = true;
globalvar define rootBall;

globalvar define thrower = null;
// 0 = hammer
// 1 = bump
// 2 = charge
// 3 = fire
// 4 = power hammer
globalvar define throwType = 0;

globalvar define ballEffect !;
globalvar define ballIcon !;

globalvar define ball;
globalvar define ballReady;

define ballIsGettingCharged: HasStatus(ball1, Status.Stunned) || HasStatus(ball2, Status.Stunned);

globalvar Color[] ballColors = [Color.Blue, Color.Green, Color.LimeGreen, Color.Orange, Color.Purple, Color.Red, Color.SkyBlue, Color.Turquoise, Color.White, Color.Yellow, Color.Aqua, Color.Black, Color.Gray, Color.Rose, Color.Violet];

rule: "Ball: Setup"
if (gamemode == Mode.Soccer || gamemode == Mode.Volleyball)
{
    ball = true;

    CreateDummyBot(Hero.WreckingBall, Team.Team1, 5, TopPolePos());
    ball1 = LastCreatedEntity();
    ball1.isBall = true;
    ball1.isWorkingDummy = true;
    rootBall = ball1;

    CreateDummyBot(Hero.WreckingBall, Team.Team2, 5, TopPolePos() + Vector(0, 1, 0));
    ball2 = LastCreatedEntity();
    ball2.isBall = true;
    ball2.isWorkingDummy = true;

    SetStatus([ball1, ball2], null, Status.Unkillable, 9999);
    DisableMovementCollisionWithPlayers([ball1, ball2]);
    StartScalingPlayer([ball1, ball2], IsInAir(rootBall) ? 2.2 : 1.1, true);
    SetInvisible([ball1, ball2], InvisibleTo.All);

    RenderBall();
    ballReady = true;
}

rule: "Ball: Clean up"
if (gamemode != Mode.Soccer)
if (gamemode != Mode.Volleyball)
{
    ball = false;
    ballReady = false;
    DestroyEffect(ballEffect);
    DestroyDummyBot(TeamOf(ball1), SlotOf(ball1));
    DestroyDummyBot(TeamOf(ball2), SlotOf(ball2));
    DestroyIcon(ballIcon);
    rootBall = null;
}

void RenderBall() "Ball: Render"
{
    // Destroy the effect and icon.
    DestroyEffect(ballEffect);
    DestroyIcon(ballIcon);

    // Create and store the effect.
    CreateEffect(AllPlayers(), Effect.Sphere, ballColors[Ball_Color.Value], UpdateEveryFrame(ballLocation()), 1.25 * ballScale, EffectRev.VisibleToPositionAndRadius);
    ballEffect = LastCreatedEntity();

    // Create the icon
    CreateIcon(FilteredArray(AllPlayers(), ballReady && !IsInViewAngle(ArrayElement(), rootBall, 30)), ballLocation(), Icon.Circle, IconRev.VisibleToAndPosition, ballColors[Ball_Color.Value], true);

    ballIcon = LastCreatedEntity();
}

globalvar define ballScale = 1;

define ballLocation(): rootBall != null ? rootBall + Vector(0, 1, 0) : Midpoint(ball1,ball2) + Vector(0, 1, 0);
define ballSpeed(): SpeedOf(rootBall);

rule: "Ball: Link"
if (ball)
if (ballReady)
if (!ballIsOnStand)
{
    Teleport(rootBall == ball1 ? ball2 : ball1, rootBall);
    Wait(0.1);
    LoopIfConditionIsTrue();
}

rule: "Ball: Respawn ball"
if (ballReady)
if (HasSpawned(ball1))
if (HasSpawned(ball2))
if (IsDead(ball1) || IsDead(ball2) || YOf(PositionOf(rootBall)) < 140)
{
    SmallMessage(AllPlayers(), "Respawning Ball");
    respawnBall();
    MinWait();
    LoopIfConditionIsTrue();
}

rule: "Ball: Bounce back ball"
if (ballReady)
if (!ballIsOnStand)
if (!HasStatus(rootBall, Status.Stunned))
// if (ballSpeed() < 1)
if (isOnWall(rootBall))
{
    ApplyImpulse(rootBall, DirectionTowards(PositionOf(rootBall), TopPolePos() + Vector(0, 20, 0)), 15, Relative.ToWorld, ContraryMotion.Cancel);
    MinWait();
    LoopIfConditionIsTrue();
}

void respawnBall() "Respawn Ball"
{
    ballReady = false;    

    stopNearbyCharging();

    Teleport([ball1,ball2], ObjectivePosition(0));
    Respawn(ball1);
    Respawn(ball2);
    Wait(1);

    // SetGravity([ball1, ball2], 0);
    Teleport(ball1, TopPolePos());
    Teleport(ball2, TopPolePos() + Vector(0, 1, 0));
    MinWait();

    ballIsOnStand = true;
    ballReady = true;
}

void stopNearbyCharging() "Stop Nearby Charging"
{
    // Stop nearby charging reinhardts nearby
    if (ballIsGettingCharged)
    {
        CancelPrimaryAction(FilteredArray(AllPlayers(), HeroOf(ArrayElement()) == Hero.Reinhardt && IsUsingAbility1(ArrayElement()) && DistanceBetween(ArrayElement(), rootBall) < 3));
        MinWait();
    }
}

rule: "Ball: On Ball Hit"
Event.OnDamageTaken
if (isBall)
if (!IsInSpawnRoom(ball1) && !IsInSpawnRoom(ball2))
{
    rootBall = EventPlayer();
    SmallMessage(AllPlayers(), EventDamage());
    Heal(EventPlayer(), null, EventDamage());

    // Hammer hit
    if (EventDamage() == 80)
    {
        ApplyImpulse(EventPlayer(), Vector(0, 1, 0), 15, Relative.ToWorld, ContraryMotion.Incorporate);
        thrower = Attacker();
        throwType = 0;
        OffStand();
    }
    // Charge bump
    else if (EventDamage() == 45)
    {
        thrower = Attacker();
        throwType = 1;
        OffStand();
    }
}

rule: "Ball: On Stun"
Event.OngoingPlayer
if (isBall)
if (HasStatus(EventPlayer(), Status.Stunned))
{
    rootBall = EventPlayer();
}

globalvar define fuse;

rule: "Ball: On Fire Strike: Impulse"
Event.OnDamageTaken
if (isBall)
if (!ballIsOnStand)
if (!ballOnGround)
if (EventDamage() == 95)
{
    rootBall = EventPlayer();
    ApplyImpulse(rootBall, Attacker().fireStrikeDirection, SpeedOf(rootBall) * 3.5, Relative.ToWorld, ContraryMotion.Cancel);
    thrower = Attacker();
    throwType = 3;
}

rule: "Ball: On Fire Strike: Detonate"
Event.OnDamageTaken
if (isBall)
if (EventDamage() == 95)
if (Ball_Explosion.Value)
{
    fuse = 0;
    ChaseVariableOverTime(fuse, 2, 3, TimeChaseReevaluation.None);

    CreateEffect(AllPlayers(), Effect.BadAura, Color.Orange, ballLocation(), fuse, EffectRev.VisibleToPositionAndRadius);
    define fuseEffect = LastCreatedEntity();
    Wait(3, WaitBehavior.IgnoreCondition);
    DestroyEffect(fuseEffect);

    StopChasingVariable(fuse);

    PlayEffect(AllPlayers(), PlayEffect.GoodExplosion, Color.Orange, ballLocation(), ExplosionRange());
    PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.Orange, ballLocation(), 100);

    define inExplosionRange = <Player[]>FilteredArray(AllPlayers(), DistanceBetween(ArrayElement(), ballLocation()) < ExplosionRange() && !ArrayElement().isBall);

    Damage(inExplosionRange, Attacker(), 200);
    inExplosionRange.knockedFrom = ballLocation();
    inExplosionRange.force = 20;
    inExplosionRange.heightOffset = 1.5;
}
define ExplosionRange(): 10;

rule: "Ball: Getting charged"
Event.OngoingPlayer
if (isBall)
if (HasStatus(EventPlayer(), Status.Stunned))
{
    define closest = 
        FirstOf(FilteredArray(
            AllPlayers(),
            HeroOf(ArrayElement()) == Hero.Reinhardt && DistanceBetween(ArrayElement(), EventPlayer()) < 3 && IsUsingAbility1(ArrayElement())
        ));

    if (closest != null)
    {
        thrower = closest;
        throwType = 2;
        OffStand();
    }
}

rule: "Ball: Effect when ball hits ground"
if (ball)
if (IsOnGround(rootBall))
{
    PlayEffect(AllPlayers(), PlayEffect.RingExplosion, ballColors[Ball_Color.Value], PositionOf(rootBall), 5);
}

rule: "Ball: Fast ball effect"
if (ball)
if (!ballIsOnStand)
if (ballSpeed() >= 10 || IsInAir(rootBall))
{
    PlayEffect(FilteredArray(AllPlayers(), DistanceBetween(ArrayElement(), rootBall) > 6), PlayEffect.BadExplosion, ballColors[Ball_Trail_Color.Value], ballLocation(), 0.3);
    MinWait();
    LoopIfConditionIsTrue();
}

void OffStand()
{
    ballIsOnStand = false;
    // SetGravity([ball1, ball2], 100);
}

globalvar define lastCrutchAction ! = 0;

disabled rule: "Ball: Shoot At Team"
if (AbsoluteValue(blueScore - redScore) >= 2)
if (!HasStatus(ball1, Status.Stunned))
if (!HasStatus(ball2, Status.Stunned))
if (!IsInAir(rootBall))
if (TotalTimeElapsed() - lastCrutchAction > 10)
{
    define shootingAtTeam ! = blueScore > redScore ? Team.Team1 : Team.Team2;
    define shootingBall ! = shootingAtTeam == Team.Team1 ? ball2 : ball1;

    // Start the ultimate
    SetUltimateCharge(shootingBall, 100);
    PressButton(shootingBall, Button.Ultimate);
    SetDamageDealt(shootingBall, 300);

    // Fire at closest enemy player for 3 seconds
    StartFacing(
        shootingBall,
        DirectionTowards(EyePosition(shootingBall), EyePosition(FirstOf(SortedArray(
            FilteredArray(AllPlayers(), !ArrayElement().isWorkingDummy && TeamOf(ArrayElement()) == shootingAtTeam),
            DistanceBetween(ArrayElement(), shootingBall)
        )))),
        750,
        Relative.ToWorld,
        FacingRev.DirectionAndTurnRate
    );
    StartHoldingButton(shootingBall, Button.PrimaryFire);
    Wait(3);

    StopHoldingButton(shootingBall);
    SetDamageDealt(shootingBall, 100);
    StopFacing(shootingBall);

    lastCrutchAction = TotalTimeElapsed();
}

globalvar BallBoundary[] BallBoundaries = [
    new BallBoundary(towerBlue, towerRed),
    new BallBoundary(cliffRed, cliffBlue),
    new BallBoundary(Vector(21.18, 154.88, 213.74), Vector(16.51, 154.88, 202.31), 0, true),
    new BallBoundary(Vector(-16.51, 154.88, 202.31), Vector(-21.18, 154.88, 213.74), 0, true)
];

class BallBoundary
{
    public Vector Pos1;
    public Vector Pos2;
    public define Distance;
    private Vector Direction;
    private define _limitDistance;

    public constructor(in Vector pos1, in Vector pos2, in define distance = -4, in define limitDistance = false)
    {
        Pos1 = pos1;
        Pos2 = pos2;
        Distance = distance;
        Direction = Normalize(CrossProduct(pos1.DirectionTowards(pos2), Up()));
        _limitDistance = limitDistance;
    }

    public void Pulse()
    {
        ApplyImpulse([root.ball1, root.ball2], Direction, Max(5, SpeedOf(root.rootBall)), Relative.ToWorld, ContraryMotion.Cancel);
    }

    public define IsInRange: (!_limitDistance || (PositionOf(root.rootBall).DistanceTo((Pos1 + Pos2) / 2) < DistanceBetween(Pos1, Pos2))) && root.signedDistance(PositionOf(root.rootBall), Pos1, Direction) < Distance;
}

rule: "Ball boundary pulse"
if (ball)
if (ballReady)
if (!ballIsOnStand)
if (BallBoundaries.IsTrueForAny((BallBoundary boundary) => boundary.IsInRange))
{
    BallBoundary inRange = BallBoundaries.FilteredArray((BallBoundary boundary) => boundary.IsInRange).First;
    inRange.Pulse();
    stopNearbyCharging();
    Wait(0.3);
    LoopIfConditionIsTrue();
}