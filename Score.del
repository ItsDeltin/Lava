import "Ball.del";

define globalvar goals;
define globalvar blueGoalEffect !;
define globalvar redGoalEffect !;

define globalvar blueScore = 0;
define globalvar redScore = 0;

macro blueGoal(): Vector(120, 165, 150);
macro redGoal(): Vector(-123, 165, 150);

rule: "Spawn goals"
if (goals)
{
    // Blue goal
    CreateEffect(
        AllPlayers(),
        Effect.GoodAura,
        Color.Team1,
        blueGoal(),
        10
    );
    blueGoalEffect = LastCreatedEntity();

    // Red goal
    CreateEffect(
        AllPlayers(),
        Effect.GoodAura,
        Color.Team2,
        redGoal(),
        10
    );
    redGoalEffect = LastCreatedEntity();
}

rule: "Destroy goals"
if (!goals)
{
    DestroyEffect(blueGoalEffect);
    DestroyEffect(redGoalEffect);
}

rule: "Blue score"
if (ballReady)
if (goals)
if (DistanceBetween(ballLocation(), redGoal()) <= 13)
{
    PlayEffect(AllPlayers(), PlayEffect.RingExplosion, Color.Team1, redGoal(), 100);
    PlayEffect(AllPlayers(), PlayEffect.GoodExplosion, Color.Team1, redGoal(), 10);
    PlayEffect(AllPlayers(Team.Team1), PlayEffect.BuffExplosionSound, Color.White, redGoal(), 1000);
    PlayEffect(AllPlayers(Team.Team2), PlayEffect.DebuffImpactSound, Color.White, redGoal(), 1000);

    blueScore++;
    messageThrower(Team.Team1);
}

rule: "Red score"
if (ballReady)
if (goals)
if (DistanceBetween(ballLocation(), blueGoal()) <= 13)
{
    PlayEffect(AllPlayers(), PlayEffect.RingExplosion, Color.Team2, blueGoal(), 100);
    PlayEffect(AllPlayers(), PlayEffect.GoodExplosion, Color.Team2, blueGoal(), 10);
    PlayEffect(AllPlayers(Team.Team2), PlayEffect.BuffExplosionSound, Color.White, blueGoal(), 1000);
    PlayEffect(AllPlayers(Team.Team1), PlayEffect.DebuffImpactSound, Color.White, blueGoal(), 1000);

    redScore++;
    messageThrower(Team.Team2);
}

method messageThrower(define team)
{
    if (TeamOf(thrower) != team)
    {
        BigMessage(AllPlayers(), <"Wrong goal, <0>!", thrower>);
    }
    else if (throwType == 0)
    {
        BigMessage(AllPlayers(), <"<0> scored a hammer shot!", thrower>);
    }
    else if (throwType == 1)
    {
        BigMessage(AllPlayers(), <"<0> scored a bump shot!", thrower>);
    }
    else if (throwType == 2)
    {
        BigMessage(AllPlayers(), <"<0> scored a charge shot!", thrower>);
    }
}