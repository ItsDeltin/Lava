import "Ball.del";

globalvar define blueGoalEffect !;
globalvar define redGoalEffect !;

globalvar define blueScore = 0;
globalvar define redScore = 0;
globalvar define totalScore = 0;

define blueGoal(): Vector(123, 165, 150);
define redGoal(): Vector(-123, 165, 150);

rule: "Switch gamemode"
if (totalScore != 0)
if (totalScore % 3 == 0)
{
    if (gamemode == Mode.Volleyball) gamemode = Mode.SoccerCountdown;
    else gamemode = Mode.Volleyball;
}

rule: "Football: Countdown"
if (gamemode == 1)
{
    AllPlayers().inputEnabled = false;

    InitSpawns();

    AllPlayers().doRespawn = true;
    respawnBall();
    for (define i = 5; i > 0; i--)
    {
        SmallMessage(AllPlayers(), i);
        Wait(1);
    }
    BigMessage(AllPlayers(), "Start!");
    AllPlayers().inputEnabled = true;

    gamemode = 2;
}

void InitSpawns()
{
    Heal(AllPlayers(), null, 9999);
    for (define i = 0; i < 5; i++)
    {
        Teleport(PlayersInSlot(i, Team.Team1), Vector(60, 154.88, (150 - 30) + (((150 + 30) - (150 - 30))/5)*i));
        Teleport(PlayersInSlot(i, Team.Team2), Vector(-60, 154.88, (150 - 30) + (((150 + 30) - (150 - 30))/5)*i));
    }
}

rule: "Football: Start/Stop"
if (IsCommunicating(HostPlayer(), Communication.VoiceLineUp))
{
    if (gamemode != 1)
        gamemode = 1;
    else gamemode = 0;
}


globalvar define blueGoalSize;
globalvar define redGoalSize;
define considerBallPos: PositionOf(rootBall);
define goalMod: Max(1 - (DistanceBetween(rootBall, blueNearest) / 15), 0) * 10;
Vector nearestPlanePoint(define pointPosition, define planePosition, define planeNormal): pointPosition + (-DotProduct(planeNormal, pointPosition - planePosition) / DotProduct(planeNormal, planeNormal)) * planeNormal;
Vector bluePos: Vector(115.69, 156.68, 148.52); // 103.43
Vector blueNormal: Vector(-0.95, 0.31, -0.02);
Vector rightOf(Vector dir): Normalize(dir.CrossProduct(Up()));
Vector upOf(Vector dir): Normalize(dir.CrossProduct(dir.CrossProduct(Up())));
Vector blueNearest: nearestPlanePoint(considerBallPos, bluePos, blueNormal);
Vector signedDistance(define pointPosition, define planePoint, define planeNormal): DotProduct(planeNormal, (pointPosition - planePoint));

define g_down(define p, define d): p + upOf(d) * blueGoalSize;
define g_right(define p, define d): p - rightOf(d) * blueGoalSize;
define g_up(define p, define d): p - upOf(d) * blueGoalSize;
define g_left(define p, define d): p + rightOf(d) * blueGoalSize;

define MakeBeam(Color color, ref define s, ref define e)
{
    CreateBeamEffect(AllPlayers(), BeamType.GoodBeam, s, e, color, EffectRev.VisibleToPositionAndRadius);
    return LastCreatedEntity();
}

void MakeBeams(Color color, ref define nearest, ref define normal)
{
    MakeBeam(color, g_up(nearest, normal), g_right(nearest, normal));
    MakeBeam(color, g_right(nearest, normal), g_down(nearest, normal));
    MakeBeam(color, g_down(nearest, normal), g_left(nearest, normal));
    MakeBeam(color, g_left(nearest, normal), g_up(nearest, normal));
}

void ChaseGoalEffects() "Chase Goal Effects"
{
    blueGoalSize = 0;
    ChaseVariableAtRate(blueGoalSize, goalMod, 10, RateChaseReevaluation.DestinationAndRate);
}

rule: "Football: Spawn goals"
if (gamemode == Mode.SoccerCountdown || gamemode == Mode.Soccer)
{
    ChaseGoalEffects();
    MakeBeams(Color.Team1, blueNearest, blueNormal);

    // CreateEffect(AllPlayers(), Effect.Orb, Color.Yellow, blueNearest, 0.5, EffectRev.VisibleToPositionAndRadius); // For debugging
    // CreateHudText(AllPlayers(), <"SD: <0>, BR: <1>, GAMEMODE: <2>", signedDistance(considerBallPos, bluePos, blueNormal) < 0, ballReady, gamemode == Mode.Soccer>);
}

rule: "Football: Destroy goals"
if (gamemode != Mode.SoccerCountdown)
if (gamemode != Mode.Soccer)
{
    DestroyEffect(blueGoalEffect);
    DestroyEffect(redGoalEffect);
}

rule: "Football: Blue score"
if (ballReady)
if (gamemode == Mode.Soccer)
if (DistanceBetween(rootBall, redGoal()) <= 13)
{
    PlayEffect(AllPlayers(), PlayEffect.RingExplosion, Color.Team1, redGoal(), 100);
    PlayEffect(AllPlayers(), PlayEffect.GoodExplosion, Color.Team1, redGoal(), 10);
    PlayEffect(AllPlayers(Team.Team1), PlayEffect.BuffExplosionSound, Color.White, redGoal(), 1000);
    PlayEffect(AllPlayers(Team.Team2), PlayEffect.DebuffImpactSound, Color.White, redGoal(), 1000);

    blueScore++;
    totalScore++;
    messageThrower(Team.Team1);
}

rule: "Football: Red score"
if (ballReady)
if (gamemode == Mode.Soccer)
if (signedDistance(considerBallPos, bluePos, blueNormal) < 0)
{
    // PlayEffect(AllPlayers(), PlayEffect.RingExplosion, Color.Team2, blueGoal(), 100);
    // PlayEffect(AllPlayers(), PlayEffect.GoodExplosion, Color.Team2, blueGoal(), 10);
    PlayEffect(AllPlayers(Team.Team2), PlayEffect.BuffExplosionSound, Color.White, blueGoal(), 1000);
    PlayEffect(AllPlayers(Team.Team1), PlayEffect.DebuffImpactSound, Color.White, blueGoal(), 1000);

    redScore++;
    totalScore++;

    ChaseVariableOverTime(blueGoalSize, 0, 0.35, TimeChaseReevaluation.None);
    Wait(0.35);
    PlayEffect(AllPlayers(), PlayEffect.RingExplosion, Color.Team2, blueNearest, 100);

    messageThrower(Team.Team2);

    Wait(5);
    ChaseGoalEffects();
}

void messageThrower(define team)
{
    if (TeamOf(thrower) != team)
    {
        BigMessage(AllPlayers(), <"Wrong goal, <0>!", thrower>);
    }
    else if (throwType == 0)
    {
        BigMessage(AllPlayers(), <"<0> scored a hammer shot!", thrower>);
    }
    else if (throwType == 1)
    {
        BigMessage(AllPlayers(), <"<0> scored a bump shot!", thrower>);
    }
    else if (throwType == 2)
    {
        BigMessage(AllPlayers(), <"<0> scored a charge shot!", thrower>);
    }
}